;===写死的文件长度,注意更改!
org 0x7c6a

movzx ebx,WORD [0x7c0e];保留扇区数
movzx eax,BYTE [0x7c10];FAT数目
mul DWORD [0x7c24] ;把fat数目和fat长度相乘
add eax,ebx;把保留扇区和fat总长度相加
add eax,8 ;增加1个簇位置(ROOTDIR)
mov DWORD [pck_sec],eax

mov ax,0x0600
mov bx,0x0700
mov cx,0x00
mov dx,0x184f
int 0x10

mov ax,0x00
mov ds,ax
mov ax,DskPack
mov si,ax
mov dl,0x80
mov ah,0x42
int 0x13
jc Fail
jnc Succ
Fail:
    mov cx,25
    mov dh,0x00
    mov dl,0x00
    mov bx,Failmsg
    call ShowMessage
    jmp $
Succ:
    mov cx,28
    mov dh,0x01
    mov dl,0x00
    mov bx,Succmsg
    call ShowMessage
    jmp 0x0000:0x7e00
    jmp $
DskPack:
db 0x10
db 0x00
dw 4
dw 0x7e00
dw 0x0000
pck_sec:
dq 4128
; cx: length, dh: row, dl: col, bx: string addr
ShowMessage:
    mov ax, ds
    mov es, ax
    mov bp, bx
    mov ax, 0x1301
    mov bx, 0x000f
    int 0x10
    ret
Failmsg:
    db "[Boot]Cannot Read Loader."
Succmsg:
    db "[Boot]Found Loader,Jumping."
times 210 db 0x00
dw 0xaa55